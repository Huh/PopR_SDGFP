	#  Simple DAU creation IPM using only prior estimates and ignoring 
	#  covariances
    model{
				
		#  Priors on pregnancy
		Preg[1] ~ dnorm(0.53, 1/(0.05^2))T(0,1)
		Preg[2] ~ dnorm(0.91, 1/(0.03^2))T(0,1)
		
		#  Priors on fetus counts
		FC[1] ~ dnorm(1.54, 1/(0.07^2))T(0,)
		FC[2] ~ dnorm(1.78, 1/(0.05^2))T(0,)
		
		#  Priors on survival fawns - from known fate so transformation is 
		#  different for first row, uses logit, ilogit because harvest is not
		#  possible per the model definition
		for(s in 1:2){
			muS[1,s] ~ dnorm(saSurv_mat[1,s], saPrec_mat[1,s])T(0, 1)
			muO[1,s] <- 1 - muS[1,s]
			muH[1,s] <- 0
		}
		
		for(s in 1:2){
			realS[1,s] <- log(muS[1,s]/(1 - muS[1,s]))
			realO[1,s] <- log(muO[1,s]/(1 - muO[1,s]))		
			realH[1,s] <- 0
			for(yr in 1:nyr){
				logit(S[yr,1,s]) <- realS[1,s] 
				logit(O[yr,1,s]) <- realO[1,s]
				H[yr,1,s] <- 0
			}
		}
		
		#  Priors on adult's and juvenile's survival
		#  Values enter as annual probabilities
		for(a in 2:nage){
			for(s in 1:2){
				muS[a, s] ~ dnorm(saSurv_mat[a, s], saPrec_mat[a, s])T(0,1)
				muH[a,s] ~ dnorm(saHarv_mat[a, s], saHPrec_mat[a, s])T(0,1) 
				muO[a, s] ~ dnorm(saOmort_mat[a, s], saOPrec_mat[a, s])T(0,1)				

			}
		}
		
		#  Transform S, O, and H to real scale - prob to multinomial lp
		for(a in 2:nage){
			for(s in 1:2){
				realS[a,s] <- log(muS[a,s]/muO[a,s])
				realH[a,s] <- log(muH[a,s]/muO[a,s])
				realO[a,s] <- log(muO[a,s]/muO[a,s])
			}
		}
		
		#  Random effect of year
		mu.epsilon[1] <- 0
		mu.epsilon[2] <- 0
		
		# V: diagonal matrix
		V[1,1] <- 2
		V[2,2] <- 2
		V[1,2] <- 0
		V[2,1] <- 0
		
		# prior on precision matrix with 3 degree of freedom
		omega[1:2, 1:2] ~ dwish(V[ , ], 3)
		
		# Covariance matrix
		sigma2[1:2, 1:2] <- inverse(omega[,])
		
		# Random month effect
		for(t in 1:nyr){
		  eps[t, 1:2] ~ dmnorm(mu.epsilon[], omega[1:2,1:2])
		  epsilon[t, 1] <- eps[t, 1]
		  epsilon[t, 2] <- eps[t, 2]
		}
        
		# Conditional standard deviation
		sigma[1] <- sqrt(sigma2[1, 1])	# survival mortality
		sigma[2] <- sqrt(sigma2[2, 2])	# hunting mortality
		
		# Conditional correlation
		rho <- sigma2[1, 2]/(sigma[1] * sigma[2])
						
		#  Setup linear predictors on survival/harvest using mlogit link	
		for(yr in 1:nyr){
			for(a in 2:nage){
				for(s in 1:2){
					psiS[yr,a,s] <- exp(realS[a,s] + epsilon[yr,1])
					psiH[yr,a,s] <- exp(realH[a,s] + epsilon[yr,2])
					psiO[yr,a,s] <- exp(realO[a,s])
					sumpsi[yr,a,s] <- psiS[yr,a,s] + psiO[yr,a,s] + psiH[yr,a,s]
					S[yr,a,s] <- psiS[yr,a,s]/sumpsi[yr,a,s]
					H[yr,a,s] <- psiH[yr,a,s]/sumpsi[yr,a,s]
					O[yr,a,s] <- psiO[yr,a,s]/sumpsi[yr,a,s]
				}
			}
		}
		
		#  Prior on first year population size
		for(a in 1:nage){
			for(s in 1:2){
				N[1, a, s] ~ dnorm(muN1[1, a, s], 0.0001)T(0, )
			}
		}

		#  Totals by sex
		Ny[1] <- N[1,1,1] + N[1,1,2]
		Nm[1] <- N[1,1,2] + N[1,2,2] + N[1,3,2]
		Nf[1] <- N[1,1,1] + N[1,2,1] + N[1,3,1]
		Ntot[1] <- Nf[1] + Nf[1]
		
		#  Process model - 3 stage, 2 sex
		for(yr in 2:nyr){
			for(s in 1:2){
				#  Fawns
				mu_y[yr, s] <- 
					((N[yr-1,2,1] * Preg[1] * FC[1]/2 * (S[yr-1,2,1]^(8/12))) + 
					(N[yr-1,3,1] * Preg[2] * FC[2]/2 * (S[yr-1,3,1]^(8/12)))) * 
					S[yr-1,1,s]
				tau_y[yr, s] <- 1/(mu_y[yr, s])
				N[yr,1,s] ~ dnorm(mu_y[yr, s], tau_y[yr, s])

				#  Juveniles
				mu_j[yr, s] <- S[yr-1,2,s] * N[yr-1,1,s]
				tau_j[yr, s] <- 1/(mu_j[yr,s] * (1 - S[yr-1,2,s]))
				N[yr,2,s] ~ dnorm(mu_j[yr,s], tau_j[yr,s])
			
				#  Adults
				mu_a[yr,s] <- S[yr-1,3,s] * (N[yr-1,2,s] + N[yr-1,3,s])
				tau_a[yr,s] <- 1/(mu_a[yr,s] * (1 - S[yr-1,3,s]))
				N[yr,3,s] ~ dnorm(mu_a[yr,s], tau_a[yr,s])
		
			}
		#  Total
		Ny[yr] <- N[yr,1,1] + N[yr,1,2]
		Nm[yr] <- N[yr,1,2] + N[yr,2,2] + N[yr,3,2]
		Nf[yr] <- N[yr,1,1] + N[yr,2,1] + N[yr,3,1]
		Ntot[yr] <- Nf[yr] + Nf[yr]
		}

		####################  Observation Models
		#  Abundance Observation
		#for(i in 1:nair){
		#	counts[i,1] ~ dnorm(Ntot[counts[i,3]], counts[i,4])
		#}

		#  Harvest Observations - [yr,a,s]
		for(i in 1:nharv){
			hd[i,1] ~ dbin(H[hd[i,3],hd[i,4],hd[i,5]], 
				round(N[hd[i,3],hd[i,4],hd[i,5]]))
		}
		
		#  Fawn:Doe Ratio Observations
		for(i in 1:nyf){
			yfdat[i,1] ~ dnorm(R[yfdat[i,3]], yfdat[i,4])T(0,2)
		}
		
		#  Buck:Doe Ratio Observations
		for(i in 1:nmf){
			mfdat[i,1] ~ dnorm(mf[mfdat[i,3]], mfdat[i,4])T(0,2)
		}
		
		#  Survival Observations - [a,s]
		for(i in 1:nS){
			Sdat[i,1] ~ dnorm(S[Sdat[i,3],Sdat[i,4],Sdat[i,5]], 
				Sdat[i,6])T(0,1)
		}
		
		for(i in 1:nHm){
			Hmdat[i,1] ~ dnorm(H[Hmdat[i,3],Hmdat[i,4],Hmdat[i,5]], 
				Hmdat[i, 6])T(0, 1)
		}

		for(i in 1:nOm){
			Omdat[i,1] ~ dnorm(O[Omdat[i,3], Omdat[i,4],Omdat[i,5]], 
				Omdat[i, 6])T(0, 1)
		}
		
		#  Derived 
		for(yr in 1:nyr){
			R[yr] <-  (N[yr,1,2] + N[yr,1,1] + 0.001)/
				(N[yr,2,1] + N[yr,3,1] + 0.001)
			mf[yr] <- (N[yr,2,2] + N[yr,3,2] + 0.001)/
				(N[yr,2,1] + N[yr,3,1] + 0.001)
		}

		lambda[1] <- 1
		for(yr in 2:nyr){
			lambda[yr] <- (Ntot[yr] + 0.001)/(Ntot[yr-1] + 0.001)
			logla[yr] <- log(lambda[yr])
		}
		geoLambda <- exp((1/(nyr-1))*sum(logla[2:(nyr)])) 
		
		#  For reporting
		for(yr in 1:nyr){
			yfS[yr] <- S[yr,1,1]
			jfS[yr] <- S[yr,2,1]
			fS[yr] <- S[yr,3,1]
			ymS[yr] <- S[yr,1,2]
			jmS[yr] <- S[yr,2,2]
			mS[yr] <- S[yr,3,2]		
			yfH[yr] <- H[yr,1,1]
			jfH[yr] <- H[yr,2,1]
			fH[yr] <- H[yr,3,1]
			ymH[yr] <- H[yr,1,2]
			jmH[yr] <- H[yr,2,2]
			mH[yr] <- H[yr,3,2]				
		}		
		
	}