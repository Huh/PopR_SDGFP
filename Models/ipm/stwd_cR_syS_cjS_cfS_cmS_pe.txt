	#  Simple DAU creation IPM using only prior estimates and ignoring 
	#  covariances
    model{
				
		#  Priors on pregnancy
		Preg[1] ~ dnorm(0.53, 1/(0.05^2))T(0,1)
		Preg[2] ~ dnorm(0.91, 1/(0.03^2))T(0,1)
		
		#  Priors on fetus counts
		FC[1] ~ dnorm(1.54, 1/(0.07^2))T(0,)
		FC[2] ~ dnorm(1.78, 1/(0.05^2))T(0,)
		
		#  Priors on survival fawns - from known fate so transformation is 
		#  different for first row, uses logit, ilogit because harvest is not
		#  possible per the model definition
		for(s in 1:2){
			muS[1,s] ~ dnorm(saSurv_mat[1,s], saPrec_mat[1,s])T(0, 1)
			muO[1,s] <- 1 - muS[1,s]
			muH[1,s] <- 0
		}
		
		#  Completion of the transformation and linear predictor
		#  This separation is necessary to accomodate covariates in the future
		#  Random effect on spatial unit
		tau <- 1/(sig ^ 2)
		sig ~ dunif(0, 20)
		for(u in 1:n_units){
			epsilon[u] ~ dnorm(0, tau)
		}
		
		for(s in 1:2){
			realS[1,s] <- log(muS[1,s]/(1 - muS[1,s]))
			realO[1,s] <- log(muO[1,s]/(1 - muO[1,s]))		
			realH[1,s] <- 0
			for(u in 1:n_units){
				for(y in 1:nyr){
					logit(S[u,y,1, s]) <- realS[1,s] + epsilon[u]  
					logit(O[u,y,1, s]) <- realO[1,s]
					H[u,y,1,s] <- 0
				}
			}
		}
		
		#  Priors on survival of adults and juveniles
		#  Values enter as annual probabilities [0,1]
		for(a in 2:nage){
			for(s in 1:2){
				muS[a, s] ~ dnorm(saSurv_mat[a, s], saPrec_mat[a, s])T(0,1)
				muH[a,s] ~ dnorm(saHarv_mat[a, s], saHPrec_mat[a, s])T(0,1) 
				muO[a, s] ~ dnorm(saOmort_mat[a, s], saOPrec_mat[a, s])T(0,1)				
			}
		}
		
		#  Transform S, O, and H to real scale - prob to multinomial lp
		for(a in 2:nage){
			for(s in 1:2){
				realS[a,s] <- log(muS[a,s]/muO[a,s])
				realH[a,s] <- log(muH[a,s]/muO[a,s])
				realO[a,s] <- log(muO[a,s]/muO[a,s])
			}
		}
						
		#  Setup linear predictors on survival/harvest using mlogit link
		#  Again the separation of the steps sets us up for incorporation of 
		#  covariates and/or random effects
		for(u in 1:n_units){
			for(y in 1:nyr){
				for(a in 2:nage){
					for(s in 1:2){
						psiS[u,y,a,s] <- exp(realS[a,s])
						psiH[u,y,a,s] <- exp(realH[a,s])
						psiO[u,y,a,s] <- exp(realO[a,s])
						sumpsi[u,y,a,s] <- psiS[u,y,a,s] + psiO[u,y,a,s] + 
							psiH[u,y,a,s]
						S[u,y,a,s] <- psiS[u,y,a,s]/sumpsi[u,y,a,s]
						H[u,y,a,s] <- psiH[u,y,a,s]/sumpsi[u,y,a,s]
						O[u,y,a,s] <- psiO[u,y,a,s]/sumpsi[u,y,a,s]
					}
				}
			}
		}
		
		#  Prior on first year population size
		#  Totals by sex in first year
		for(u in 1:n_units){
			for(a in 1:nage){
				for(s in 1:2){
					N[u,1,a,s] ~ dnorm(muN1[u, a, s], 0.0001)T(0, )
				}
			}
			#  Totals by sex
			Nm[u,1] <- N[u,1,1,2] + N[u,1,2,2] + N[u,1,3,2]
			Nf[u,1] <- N[u,1,1,1] + N[u,1,2,1] + N[u,1,3,1]
			Ntot[u,1] <- Nf[u,1] + Nf[u,1]
		}
		
		#  Process model - 3 stage, 2 sex
		#  Using normal approximation because it is fast and mixes well
		#  Process model - 3 stage, 2 sex
		for(u in 1:n_units){
			for(yr in 2:nyr){
				for(s in 1:2){
					#  Fawns
					mu_y[u, yr, s] <- 
						((N[u, yr-1, 2, 1] * Preg[1] * FC[1]/2 * 
							(S[u,yr-1,2,1]^(8/12))) + 
						(N[u, yr-1, 3, 1] * Preg[2] * FC[2]/2 * 
							(S[u,yr-1,3,1]^(8/12)))) *	
						S[u, yr-1, 1, s]
					tau_y[u, yr, s] <- 1/(mu_y[u, yr, s])
					N[u,yr,1,s] ~ dnorm(mu_y[u, yr, s], tau_y[u, yr, s])

					#  Juveniles
					mu_j[u, yr, s] <- S[u,yr-1,2,s] * N[u,yr-1,1,s]
					tau_j[u, yr, s] <- 1/(mu_j[u,yr,s] * (1 - S[u,yr-1,2,s]))
					N[u,yr,2,s] ~ dnorm(mu_j[u,yr,s], tau_j[u,yr,s])

					#  Adults
					mu_a[u, yr, s] <- S[u,yr-1,3,s] * 
						(N[u,yr-1,2,s] + N[u,yr-1,3,s])
					tau_a[u, yr, s] <- 1/(mu_a[u,yr,s] * (1 - S[u,yr-1,3,s]))
					N[u,yr,3,s] ~ dnorm(mu_a[u,yr,s], tau_a[u,yr,s])
				}
			#  Total
			Nm[u,yr] <- N[u,yr,1,2] + N[u,yr,2,2] + N[u,yr,3,2]
			Nf[u,yr] <- N[u,yr,1,1] + N[u,yr,2,1] + N[u,yr,3,1]
			Ntot[u,yr] <- Nf[u,yr] + Nf[u,yr]
			}
		}

		####################  Observation Models
		#  Indexing/columns always follows
		#  1	  2    3     4    5    6		
		#  Value, DAU, Year, Age, Sex, SD/SE
		
		#  Abundance Observation - no DAU wide abundance estimates...yet
		#for(i in 1:nair){
		#	counts[i,1] ~ dnorm(Ntot[counts[i,3]], counts[i,4])
		#}

		#  Harvest Observations - [u,yr,a,s]
		for(i in 1:nharv){
			hd[i,1] ~ dbin(H[hd[i,2],hd[i,3],hd[i,4],hd[i,5]], 
						round(N[hd[i,2],hd[i,3],hd[i,4],hd[i,5]]))
		}
		
		#  Fawn:Doe Ratio Observations
		for(i in 1:nyf){
			yfdat[i,1] ~ dnorm(R[yfdat[i,2],yfdat[i,3]], yfdat[i,4])T(0,2)
		}
		
		#  Buck:Doe Ratio Observations
		for(i in 1:nmf){
			mfdat[i,1] ~ dnorm(mf[mfdat[i,2], mfdat[i,3]], mfdat[i,4])T(0,2)
		}
		
		#  Survival Observations - [u,yr,a,s]
		for(i in 1:nS){
			Sdat[i,1] ~ dnorm(S[Sdat[i,2],Sdat[i,3],Sdat[i,4],Sdat[i,5]], 
				Sdat[i,6])T(0,1)
		}
		
		for(i in 1:nHm){
			Hmdat[i,1] ~ dnorm(H[Hmdat[i,2],Hmdat[i,3],Hmdat[i,4],Hmdat[i,5]], 
				Hmdat[i, 6])T(0, 1)
		}

		for(i in 1:nOm){
			Omdat[i,1] ~ dnorm(O[Omdat[i,2],Omdat[i,3],Omdat[i,4],Omdat[i,5]], 
				Omdat[i, 6])T(0, 1)
		}
		
		#  Derived - the constant is added to avoid division by 0
		for(u in 1:n_units){
			for(yr in 1:nyr){
				R[u, yr] <-  (N[u,yr,1,2] + N[u,yr,1,1] + 0.001)/
							(N[u,yr,2,1] + N[u,yr,3,1] + 0.001)
				mf[u, yr] <- (N[u,yr,2,2] + N[u,yr,3,2] + 0.001)/
				            (N[u,yr,2,1] + N[u,yr,3,1] + 0.001)
			}
		}

		#  Incomplete vectors cannot be monitored, so aribitrary value is given
		#  to the first year
		#  Same constant trick is used here for the division
		#  Using the log and exp handles 0 gracefully, recall that  
		#  log(x) + log(y) = log(xy), so the geometric mean is calculated using
		#  an algebraic rearrangment that is more robust to 0's
		for(u in 1:n_units){		
			lambda[u, 1] <- 1
			for(yr in 2:nyr){
				lambda[u,yr] <- (Ntot[u, yr] + 0.001)/(Ntot[u, yr-1] + 0.001)
				logla[u,yr] <- log(lambda[u, yr])
			}
			geoLambda[u] <- exp((1/(nyr-1))*sum(logla[u,2:(nyr)])) 
		}
		
	}