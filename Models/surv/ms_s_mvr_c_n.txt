		#  JAGS survival model with random effect on occasion
		#  Josh Nowak
		#  07/2015
#################################################################################
      model{
        #  Priors
        #  Survival
        b0[1] ~ dnorm(5, 0.01)T(-10, 10)
        #  Hunting Mortality
        b0[2] ~ dnorm(-3.5, 0.01)T(-10, 10)
        #  Other Mortality
        b0[3] <- 0
		
		#  Monthly random effect
		mu.epsilon[1] <- 0
		mu.epsilon[2] <- 0
		
		# V: diagonal matrix
		V[1,1] <- 2
		V[2,2] <- 2
		V[1,2] <- 0
		V[2,1] <- 0
		
		# prior on precision matrix with 3 degree of freedom
		omega[1:2, 1:2] ~ dwish(V[ , ], 3)
		
		# Covariance matrix
		sigma2[1:2, 1:2] <- inverse(omega[,])
		
		# Random month effect
		for(t in 1:(nocc - 1)){
		  eps[t, 1:2] ~ dmnorm(mu.epsilon[],omega[1:2,1:2])
		  epsilon[t, 1] <- eps[t, 1]
		  epsilon[t, 2] <- eps[t, 2]
		}
        
		# Conditional standard deviation
		sigma[1] <- sqrt(sigma2[1, 1])	# survival mortality
		sigma[2] <- sqrt(sigma2[2, 2])	# hunting mortality
		
		# Conditional correlation
		rho <- sigma2[1, 2]/(sigma[1] * sigma[2])
		
		#  Linear predictor
		for(t in 1:(nocc - 1)){
			log(psi[t,1]) <- b0[1] + epsilon[t,1]
			log(psi[t,2]) <- b0[2] + epsilon[t,2]
			log(psi[t,3]) <- 0
		}
		
		#  Probability of survival, death by hunting and death by other
		for(t in 1:(nocc - 1)){
			monthly[t] <- psi[t,1]/sum(psi[t,])
			hmort[t] <- psi[t,2]/sum(psi[t,])
			nmort[t] <- psi[t,3]/sum(psi[t,])
		}
		#  Annual Rates
		Sann <- prod(monthly[])
		
		#  Cumulative Harvest Mortality
		Hcum[1] <- hmort[1]
		for(i in 2:(nocc - 1)){
			Hcum[i] <- prod(monthly[1:(i-1)]) * hmort[i] 
		}
		
		Hann <- sum(Hcum[])
		
		#  Cumulative Other Mortality
		Ocum[1] <- nmort[1]
		for(i in 2:(nocc - 1)){
			Ocum[i] <- prod(monthly[1:(i-1)]) * nmort[i] 
		}
		
		Oann <- sum(Ocum[])

		#  Mean monthly rates
		Smon <- exp(b0[1])/(exp(b0[1])+exp(b0[2])+exp(b0[3])) 
		Hmon <- exp(b0[2])/(exp(b0[1])+exp(b0[2])+exp(b0[3])) 
		Omon <- exp(b0[3])/(exp(b0[1])+exp(b0[2])+exp(b0[3])) 

		# STATE Indices
		# First index = state at time t-1
		# Second index = individual (i)
		# Third index = time (t)
		# Fourth index = state at time t
		# STATE Codes
		# 1 alive
		# 2 dead, harvested
		# 3 dead, not harvested
		# 4 dead, long time
		##################################
		# DEFINE PARAMETERS
		for (i in 1:nind){
			for (t in first[i]:(last[i]-1)){
				# Probability of X(t) given X(t-1)
				px[1,i,t,1] <- monthly[t]
				px[1,i,t,2] <- hmort[t]
				px[1,i,t,3] <- nmort[t]
				px[1,i,t,4] <- 0
				px[2,i,t,1] <- 0
				px[2,i,t,2] <- 0
				px[2,i,t,3] <- 0
				px[2,i,t,4] <- 1
				px[3,i,t,1] <- 0
				px[3,i,t,2] <- 0
				px[3,i,t,3] <- 0
				px[3,i,t,4] <- 1
				px[4,i,t,1] <- 0
				px[4,i,t,2] <- 0
				px[4,i,t,3] <- 0
				px[4,i,t,4] <- 1
			}
		}

		#  Likelihood
		for(i in 1:nind){  #  Loop over individuals
		for(t in (first[i]+1):last[i]){  #  Loop over time
				eh[i,t] ~ dcat(px[eh[i,t-1],i,t-1,1:4])
			} # t
		} # i

	}  