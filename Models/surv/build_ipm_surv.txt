		#  JAGS survival model with random effect on occasion
		#  Josh Nowak
		#  07/2015
#################################################################################
      model{
        #  Priors
	
        #  Intercept
        #  Constraint for baseline fate(3), all ages = 0 at fate 3
		alpha[3] <- 0
        for(a in 1:2){
			alpha[a] ~ dnorm(0, 0.001)
        }

        #  Effect of sex
        #  Constrain sex effect at fate 1 to 0
        b0[3] <- 0
        for(k in 1:2){
            b0[k] ~ dnorm(0, 0.001)
        }		
		
		#####
        #  Temporal random effects - monthly
        mu_month[1] <- 0
        mu_month[2] <- 0

        # V: diagonal matrix
       	V[1,1] <- 2
       	V[2,2] <- 2
       	V[1,2] <- 0
       	V[2,1] <- 0

        # prior on precision matrix with 3 degree of freedom
        omega[1:2, 1:2] ~ dwish(V[ , ], 3)

        # Covariance matrix
       	sigma2[1:2, 1:2] <- inverse(omega[,])

        # Random month effect
        for(u in 1:(nocc - 1)){
          eps[u, 1:2] ~ dmnorm(mu_month[], omega[1:2,1:2])
          epsilon[u, 1] <- eps[u, 1]
          epsilon[u, 2] <- eps[u, 2]
        }

        # Conditional standard deviation
        sigma[1] <- sqrt(sigma2[1, 1])	# survival
        sigma[2] <- sqrt(sigma2[2, 2])	# hunting mortality

        # Conditional correlation
        rho <- sigma2[1, 2] / (sigma[1] * sigma[2])

		#####
		#  Spatial random effect
        mu_unit[1] <- 0
        mu_unit[2] <- 0

        # VV: diagonal matrix
       	VV[1,1] <- 2
       	VV[2,2] <- 2
       	VV[1,2] <- 0
       	VV[2,1] <- 0

        # prior on precision matrix with 3 degree of freedom
        omega_sp[1:2, 1:2] ~ dwish(VV[ , ], 3)

        # Covariance matrix
       	sigma2_sp[1:2, 1:2] <- inverse(omega_sp[,])

        # Random unit effect
        for(u in 1:nunits){
          eps_sp[u, 1:2] ~ dmnorm(mu_unit[], omega_sp[1:2,1:2])
          epsilon_sp[u, 1] <- eps_sp[u, 1]
          epsilon_sp[u, 2] <- eps_sp[u, 2]
        }

        # Conditional standard deviation
        sigma_sp[1] <- sqrt(sigma2_sp[1, 1])	# survival
        sigma_sp[2] <- sqrt(sigma2_sp[2, 2])	# hunting mortality

        # Conditional correlation
        rho_sp <- sigma2_sp[1, 2] / (sigma_sp[1] * sigma_sp[2])
		
        #  Linear predictor
        for(u in 1:nunits){
			for(t in 1:(nocc - 1)){
				for(s in 1:2){
					tmp1[u,t,s,1] <- alpha[1] +  b0[1] * (s-1) + 
						epsilon[t,1] + epsilon_sp[u,1]
					log(psi[u,t,s,1]) <- tmp1[u,t,s,1]
					tmp2[u,t,s,1] <- alpha[2] + b0[2] * (s-1) + 
						epsilon[t,2] + epsilon_sp[u,2]
					log(psi[u,t,s,2]) <- tmp2[u,t,s,1]
					log(psi[u,t,s,3]) <- 0
				}
			}
		}
		
        #  Probability of survival, death by hunting and death by other
        #  (i.e. multinomial link function)
        for(u in 1:nunits){
			for(t in 1:(nocc - 1)){
				for(s in 1:2){
					
					monthly[u,t,s] <- psi[u,t,s,1]/sum(psi[u,t,s,])
					hmort[u,t,s] <- psi[u,t,s,2]/sum(psi[u,t,s,])
					nmort[u,t,s] <- psi[u,t,s,3]/sum(psi[u,t,s,])
				
				}
			}
		}

		#  Annual Rates
		for(u in 1:nunits){
			for(s in 1:2){
				Sann[u,s] <- prod(monthly[u, ,s])
			}
		}
		
		#  Cumulative Harvest Mortality
		for(u in 1:nunits){
			for(s in 1:2){		
				Hcum[u,1,s] <- hmort[u,1,s]
				for(t in 2:(nocc - 1)){
					Hcum[u,t,s] <- prod(monthly[u,1:(t-1),s]) * hmort[u,t,s] 
				}
				Hann[u,s] <- sum(Hcum[u,,s])
			}
		}

		
		#  Cumulative Other Mortality
		for(u in 1:nunits){
			for(s in 1:2){		
				Ocum[u,1,s] <- nmort[u,1,s]
				for(t in 2:(nocc - 1)){
					Ocum[u,t,s] <- prod(monthly[u,1:(t-1),s]) * nmort[u,t,s] 
				}
				Oann[u,s] <- sum(Ocum[u,,s])
			}
		}

		# STATE Indices
		# First index = state at time t-1
		# Second index = individual (i)
		# Third index = time (t)
		# Fourth index = state at time t
		# STATE Codes
		# 1 alive
		# 2 dead, harvested
		# 3 dead, not harvested
		# 4 dead, long time
		##################################
		# DEFINE PARAMETERS
		for (i in 1:nind){
			for (t in first[i]:(last[i]-1)){
				# Probability of X(t) given X(t-1)
				px[1,i,t,1] <- monthly[unit[i], t, sex[i]]
				px[1,i,t,2] <- hmort[unit[i], t, sex[i]]
				px[1,i,t,3] <- nmort[unit[i], t, sex[i]]
				px[1,i,t,4] <- 0
				px[2,i,t,1] <- 0
				px[2,i,t,2] <- 0
				px[2,i,t,3] <- 0
				px[2,i,t,4] <- 1
				px[3,i,t,1] <- 0
				px[3,i,t,2] <- 0
				px[3,i,t,3] <- 0
				px[3,i,t,4] <- 1
				px[4,i,t,1] <- 0
				px[4,i,t,2] <- 0
				px[4,i,t,3] <- 0
				px[4,i,t,4] <- 1
			}
		}

		#  Likelihood
		for(i in 1:nind){  #  Loop over individuals
		for(t in (first[i]+1):last[i]){  #  Loop over time
				eh[i,t] ~ dcat(px[eh[i,t-1],i,t-1,1:4])
			} # t
		} # i

	}  